<!-- Copyright (c) 2003 TrakHealth Pty Limited. ALL RIGHTS RESERVED. -->
<!-- This is a generic page used to display single simple components  -->
<csp:method name=OnPreHTTP arguments="" returntype=%Boolean>
 //i ##Class(websys.SessionEvents).SessionExpired()
 i ##Class(ext.websys.SessionEvents).SessionExpired() q 1
 quit 1
</csp:method>
<HTML XMLNS=TRAK>
<HEAD>
<TITLE><TRAK:TRANSLATE id=title>##(%session.Get("TITLE"))##</TRAK:TRANSLATE></TITLE>
<SERVER>
 ;Log 43894 10/8/04 - Set defaults on ORAnaOperation.Edit as per prefrences
 s id=$g(%request.Data("ID",1))
 i id="",$g(%request.Data("NewOper",1))'="" s %request.Data("NewFlag",1)=1
 ;Only set defaults for a new Operation
 i id="" d
 . s RBOperRoomID=""
 . i $g(%request.Data("OperRoomID",1))'="" s RBOperRoomID=$g(%request.Data("OperRoomID",1))
 . i $g(%request.Data("ANARBOPId",1))'="" s RBOperRoomID=$g(%request.Data("ANARBOPId",1))
 . s NextSecProc=$g(%request.Data("SecProcId",1))
 . d ##Class(web.RBOperatingRoom).GetRBOPOperPrefDefaults(RBOperRoomID)
 . i NextSecProc d ##Class(web.RBOperatingRoom).GetRBOPOperSecPrefDefaults(RBOperRoomID,NextSecProc)
 i id'="" d
 . s RBOperRoomID=""
 . i $g(%request.Data("OperRoomID",1))'="" s RBOperRoomID=$g(%request.Data("OperRoomID",1))
 . i $g(%request.Data("ANARBOPId",1))'="" s RBOperRoomID=$g(%request.Data("ANARBOPId",1))
 . s %request.Data("OPDescString",1)=##Class(web.ORAnOperSecProc).OperationsForBooking(id,RBOperRoomID)
 . s %request.Data("PROCDescString",1)=##Class(web.ORAnOperSecProc).ProceduresForBooking(id,RBOperRoomID)

</SERVER><!-- <TRAK:HEAD></TRAK:HEAD> -->
<EXTHEALTH:HEAD></EXTHEALTH:HEAD>
</HEAD>
<BODY>
<TRAK:APPLET></TRAK:APPLET>
<SCRIPT language="Cache" RUNAT=SERVER>
 n AnaestID,epis,ana,anaop,frm,OperRoomID,NextOperId,PrevOperId,EnableNext,NewFlag
 s (AnaestID,epis,ana,anaop,frm,OperRoomID,NextOperId,PrevOperId,EnableNext,NewFlag)=""
 i $g(%request.Data("OperRoomID",1))'="" s OperRoomID=$g(%request.Data("OperRoomID",1))
 i $g(%request.Data("ANARBOPId",1))'="" s OperRoomID=$g(%request.Data("ANARBOPId",1))
 s %request.Data("ANARBOPId",1)=OperRoomID
 s id=$g(%request.Data("ID",1))
 s NextSecProc=$g(%request.Data("SecProcId",1))
 s PrevOperId=$g(%request.Data("PrevOperId",1))
 s PARREF=$g(%request.Data("PARREF",1))
 s NewFlag=$g(%request.Data("NewFlag",1))
 ;
 i PARREF="",$g(%request.Data("AnaesthesiaID",1))'="" d
 .s PARREF=$g(%request.Data("AnaesthesiaID",1))
 .s %request.Data("PARREF",1)=PARREF
 i id'="" d
 . s %request.Data("OPDescString",1)=##Class(web.ORAnOperSecProc).OperationsForBooking(id,OperRoomID)
 . s %request.Data("PROCDescString",1)=##Class(web.ORAnOperSecProc).ProceduresForBooking(id,OperRoomID)
 ;
 ;Check if there is already an Anaesthetic record for this operation
 i PARREF="" d
 .s EpisodeID=$g(%request.Data("EpisodeID",1))
 .i OperRoomID d
 ..s epis=$o(^OR(0,"RBOP",OperRoomID,epis))
 ..i epis d
 ...s ana=$o(^OR(0,"RBOP",OperRoomID,epis,ana))
 ...i ana d
 ....s PARREF=epis_"||"_ana
 .;if there is no Anaesthetic record, then create a new one.
 .i PARREF="" d
 ..s %request.Data("PARREF",1)=EpisodeID
 ..s %request.Data("ANADate",1)=##Class(websys.Conversions).DateLogicalToHtml(+$h)
 ..s %request.Data("ANANo",1)=""
 ..d ##Class(web.ORAnaesthesia).websysSave(%request.Get("TEVENT"),"")
 ..s PARREF=$g(%request.Data("ID",1))
 ..s %request.Data("ID",1)=""
 .s %request.Data("PARREF",1)=PARREF
 ;s ^zTRAK("BR",$zn,"OperRoomID")=OperRoomID
 ;If there is an Operation ID passed in, use that, not the first one for the RBOperRoom
 i PrevOperId'="",id="" s id=PrevOperId
 
 ;This is for when you are scrolling back through the records and have arrived at this page after already pressing previous.
 i PrevOperId="",id'="" s PrevOperId=##Class(web.ORAnaestOperation).GetPrevOperation(id)
 s (epis,ana)=""
 ;Get First Operation Id for this RBOperRoom.
 i OperRoomID'="",id="",NewFlag="" d
 .s epis=$o(^OR(0,"RBOP",OperRoomID,epis))
 .i epis d
 ..s ana=$o(^OR(0,"RBOP",OperRoomID,epis,ana))
 ..i ana d
 ...s AnaestID=epis_"||"_ana
 ...s %request.Data("PARREF",1)=AnaestID 
 ...s anaop=0
 ...s anaop=$o(^OR(epis,"ANA",ana,"OP",anaop))
 ...i anaop d
 ....s id=epis_"||"_ana_"||"_anaop
 ....s %request.Data("ID",1)=id

 i id'="" d
 .s EnableNext="Y"
 .s NextOperId=##Class(web.ORAnaestOperation).GetNextOperation(id,OperRoomID)
 .s %request.Data("NextOperId",1)=$p(NextOperId,",",1)
 .s %request.Data("NextSecProc",1)=$p(NextOperId,",",2)
 .;If there is nothing returned, disable next button
 .i $p(NextOperId,",",1)="",$p(NextOperId,",",2)="" s EnableNext=""
 .;If the secproc returned is the same as the current, then the user hasn't entered the details for this proc yet, so disable next button.
 .i NextSecProc=$p(NextOperId,",",2) s EnableNext=""
 ;
 i $g(%request.Data("OPDescString",1))'="" s (EnableNext,PrevOperId)=""
 i $g(%request.Data("PROCDescString",1))'="" s (EnableNext,PrevOperId)=""
 
 s PatientBanner=$g(%request.Data("PatientBanner",1))
 s PatientID=$g(%request.Data("PatientID",1))
 i PatientID'="" d
 . s frm=##Class(websys.Component).OpenName("PAPerson.Banner")
 . i frm d frm.Show() d frm.%Close()
 s frm=##Class(websys.Component).OpenName("ORAnaOperation.Edit")
 i frm d frm.Show() d frm.%Close()
 
</SCRIPT>
<SCRIPT language=javascript>

	var objNext=document.getElementById('NextOper');	
	if (objNext) {
		if ("#(EnableNext)#"=="") {
			objNext.disabled=true;
			objNext.onclick=LinkDisable;
		}
	}
		
	var objPrev=document.getElementById("PrevOper");
	if (objPrev) {
		if ("#(PrevOperId)#"=="") {
			objPrev.disabled=true;
			objPrev.onclick=LinkDisable;
		}
	}

function LinkDisable(evt) {
	var el = websys_getSrcElement(evt);
	//if (el.id=="ViewableBy") VIEWABLE=el;
	if (el.disabled) {
		return false;
	}
	return true;
}

</SCRIPT>
</BODY>
</HTML>
