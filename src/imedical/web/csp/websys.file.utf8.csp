<script runat="server" language="cache">
	set act = $g(%request.Data("act",1))
	Set dirname = $g(%request.Data("dirname",1)) //##class(web.Util.DHCJFXMLOperate).GetPhysicalPath($g(%request.Data("dirname",1)))
	if (act="download"){
		if ($d(%request.Data("errorMsg",1), errorMsg)) {
			Write "{""fileFullName"":"""",""msg"":"""_##class(ext.util.String).EvalJSON(errorMsg)_"""}"
			Quit "" 
		}
		if (ServerType="HTTP"){
			set rtn = ##class(websys.File).HTTPDownLoad(dirname,filename,.stream)
		}else{
			set rtn = ##class(websys.File).FTPDownLoad(dirname,filename,.stream)
		}
		if (+rtn>0)&&(stream){
			d stream.Rewind()
			while('stream.AtEnd){ 
				//2018-5-14 修复pdf,xlsx等下载下来无法正确查看问题
				if $g(charset)="" w stream.Read() 
				else  w $ZCVT(stream.ReadLine(),"I","GB18030"),!
			}
			d stream.%Close()
			s stream=""
			d %response.Flush()	
		}else{
			Write "{""fileFullName"":"""",""msg"":"""_##class(ext.util.String).EvalJSON(rtn)_"""}"
		}
	} elseif(act="upload"){
		;前台文件
		If ($data(%request.MimeData("FileStream",1))){
			New fileStream
			Set fileStream = %request.MimeData("FileStream",1)
			if $g(filename)=""{
				try{ ;前台是file提交fileStream是File对象  ;用Uploadfiy提交fileStream是%CSP.BinaryStream对象 
					Set filename = fileStream.FileName
					Set filename = $p(filename,"\",$l(filename,"\"))  //在信任站点情况下 文件名会包含客户端本地的路径
					Set filename = $p(filename,"/",$l(filename,"/"))
					
					
				}catch(e){
					Set filename = "undefined.dat"
				}
			}
			if (ServerType="HTTP"){
				set rtn = ##class(websys.File).HTTPUpload(fileStream,dirname,filename)
			}else{
				set rtn = ##class(websys.File).FTPUpload(fileStream,dirname,filename)
			}
			if (+rtn>0){
				Write "{""fileFullName"":"""_##class(ext.util.String).EvalJSON($p(rtn,"^",2))_"""}"
			}else{
				Write "{""fileFullName"":"""",""msg"":"""_##class(ext.util.String).EvalJSON(rtn)_"""}"
			}
		}
	}
	Quit "" 
</script>
<script language=Cache method=OnPreHTTP arguments="" returntype=%Boolean>
	Set ServerType="HTTP"
	if $D(^oddDEF("websys.DHCMessageConfig","a","DHCMCAttaServerType")){
		Set Obj=##Class(websys.DHCMessageConfig).%OpenId(1)
		if Obj{
			Set ServerType = Obj.DHCMCAttaServerType,Obj=""
		}
	}
	if $d(%request.Data("servertype",1)) Set ServerType = $g(%request.Data("servertype",1))
	; 附件服务器与消息服务器是同一台服务器时 flash上传文件,新起会话,破坏旧会话,导致会话超时
	Set sessionid = $g(%request.Data("CSPSESSIONID",1))
	Set sessionobj = ##class(%CSP.Session).%OpenId(sessionid)
	if $IsObject(sessionobj){
		Set %session = sessionobj
		If ##Class(websys.SessionEvents).SessionExpired() q 1
	}
	;
	set filename = $g(%request.Data("filename",1))
	set act = $g(%request.Data("act",1))
	if (act="download"){
		//2018-5-14 修复pdf,xlsx等下载下来无法正确查看问题
		set ext=$p(filename,".",$l(filename,".")) 
		if ",xls,xml,xlsx,doc,docx,txt,csv,pdf,html,js,css,jpg,jpeg,png,bmp,gif,"[(","_ext_","){   //图片也改为允许
			d ##class(%CSP.StreamServer).FileClassify(ext,.type,.bin,.charset)
			s %response.CharSet=charset
			set %response.ContentType=type  
			//if filename'="" set ^cryze("filedownload",filename)=type_","_bin_","_charset
			Do %response.SetHeader("Content-Disposition", "attachment; filename="_##class(%CSP.Page).EscapeURL(filename))
		}else{
			q 0
		}
	} elseif(act="upload"){
	}
	Quit 1
</script>
<csp:content charset="utf-8">
