Class web.DHCAntCVComm Extends %RegisteredObject
{

Parameter useTrans = 1;

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpLoc","")
/// desc 代码 描述 简拼 包含串
/// locType 科室类型 CTLOCType  可多个 ,分隔
/// onlySameHosp 是否要和当前登录医院一致
/// locGroup 科室部门组 CTLOCDepDR->RBCDepartmentGroup 可多个 ,分隔
/// onlyActive 仅激活 且在开始截至日期范围内
Query LookUpLoc(desc = "", locType = "", onlySameHosp = "", locGroup = "", onlyActive = "", pAdmType = "") As websys.Query(CONTAINID = 3, ROWSPEC = "Code:%String,Description:%String,HIDDEN:%String,Hosp,HospDesc")
{
}

ClassMethod LookUpLocExecute(ByRef QHandle As %Library.Binary, desc = "", locType = "", onlySameHosp = "", locGroup = "", onlyActive = "", pAdmType = "") As %Library.Status
{
 	s repid=$I(^CacheTemp) 
 	s ind=1 s QHandle=$lb(0,repid,0)
 	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
 	
 	for i=1:1:$l(locType,","){
	 	s onetype=$p(locType,",",i)
	 	if onetype'="" s locTypeList(onetype)=1
	}
	
	for i=1:1:$l(locGroup,","){
		set onegroup=$p(locGroup,",",i),onegroupid=0
		s onegroup=$$ALPHAUP^SSUTIL4(onegroup)
		if onegroup'="" set onegroupid=$o(^RBC("DEP",0,"Desc",onegroup,0))
		if onegroupid>0 s locGroupList(onegroupid)=1
	}
	
	set sessHosp=""
	if onlySameHosp=1,$d(%session){
		set sessLoc=%session.Get("LOGON.CTLOCID")
		set sessHosp=$p(^CTLOC(sessLoc),"^",22)
	}
	s descU=$zcvt(desc,"U")
	s nowDate=+$h
 	set CTLocId=0
	for {
		set CTLocId=$o(^CTLOC(CTLocId))
		q:CTLocId=""
		set Info=^CTLOC(CTLocId)
		set Code=$p(Info,"^",1)
		set Desc=$p(Info,"^",2)
		
		if (pAdmType'="")&&('$d(^PAC("ADMLOC",0,"AdmType",pAdmType,CTLocId))) continue   //就诊类型 从User.PACAdmTypeLocation取科室  【基础数据-病人管理-访问类型位置】
		
		if useTrans s Desc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",Desc,langId)  //翻译
		
		set Spell=$p(Info,"^",43)
		continue:(descU'="")&&($zcvt(Code_","_Desc_","_Spell,"U")'[descU)
		//科室类型
		set CTLocType=$p(Info,"^",13)
		continue:$d(locTypeList)&&((CTLocType="")||('$d(locTypeList(CTLocType))))
		//科室部门组
		set CTLocDepDr=$p(Info,"^",19)
		continue:$d(locGroupList)&&((CTLocDepDr="")||('$d(locGroupList(CTLocDepDr))))
		//和当前登录科室同医院
		set CTLocHospDr=$p(Info,"^",22)
		continue:(sessHosp'="")&&(sessHosp'=CTLocHospDr)
		
		if onlyActive=1{
			set ActiveFrom=$p(Info,"^",24)
			continue:(ActiveFrom>0)&&(nowDate<ActiveFrom)
			set ActiveTo=$p(Info,"^",25)
			continue:(ActiveTo>0)&&(nowDate>ActiveTo)
		}
		set CTLocHospDesc=""
		if CTLocHospDr>0 set CTLocHospDesc=$p($g(^CT("HOSP",CTLocHospDr)),"^",2)
		
		if useTrans s CTLocHospDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTHospital","HOSPDesc",CTLocHospDesc,langId)  //翻译
		
		Set ^CacheTemp(repid,ind)=$lb(Code,Desc,CTLocId,CTLocHospDr,CTLocHospDesc)
		Set ind=ind+1
	}
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpARCItmMast")
Query LookUpARCItmMast(q = "") As websys.Query(CONTAINID = 1, ROWSPEC = "TId:%String,TCode:%String,TDesc:%String,TSpell")
{
}

/// Generated by a.util.U5 at 2019-10-18 09:47:37
ClassMethod LookUpARCItmMastExecute(ByRef QHandle As %Library.Binary, q = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	set s="",qU=$zcvt(q,"U")
	for {
		Set s=$o(^ARCIM(s))
		q:s=""
		continue:'(s>0)
		s v=""
		for {
			s v=$o(^ARCIM(s,v))	
			q:v=""
			s str1=^ARCIM(s,v,1)
			s TId=s_"||"_v
			s TCode=$p(str1,"^",1)
			s TDesc=$p(str1,"^",2)
			
			if useTrans s TDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.ARCItmMast","ARCIMDesc",TDesc,langId)
			
			s TSpell=##class(ext.util.String).ToChineseSpell(TDesc)
			continue:(q'="")&&($zcvt(TCode_","_TDesc_","_TSpell,"U")'[qU)
			
			Set ^CacheTemp(repid,ind)=$lb(TId,TCode,TDesc,TSpell)
			set ind=ind+1
		}
	}
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpCTRelation")
Query LookUpCTRelation(q = "", onlyActive = 1) As websys.Query(CONTAINID = 1, ROWSPEC = "TId:%String,TCode:%String,TDesc:%String")
{
}

/// Generated by a.util.U5 at 2019-10-18 09:47:37
ClassMethod LookUpCTRelationExecute(ByRef QHandle As %Library.Binary, q = "", onlyActive = 1) As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	s qU=$zcvt(q,"U")
	s nowDate=+$h
	
	s TId=""
	for {
		s TId=$o(^CT("RLT",TId))	
		q:TId=""
		continue:TId'>0
		s g=^CT("RLT",TId)
		s TCode=$p(g,"^",1)
		s TDesc=$p(g,"^",2)
		if onlyActive=1{
			s TDateFrom=$p(g,"^",3)
			s TDateTo=$p(g,"^",4)
			continue:(TDateFrom>0)&&(TDateFrom>nowDate)
			continue:(TDateTo>0)&&(TDateTo<nowDate)
		}
		s TSpell=##class(ext.util.String).ToChineseSpell(TDesc)
		continue:(q'="")&&($zcvt(TCode_","_TDesc_","_TSpell,"U")'[qU)
		
		Set ^CacheTemp(repid,ind)=$lb(TId,TCode,TDesc)
		set ind=ind+1
	}
	
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpUser","demo")
/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpUser","",1,1,"1","DOCTOR")
Query LookUpUser(q = "", onlyActive = 1, UseRes = "", LocId = "", CareType = "") As websys.Query(CONTAINID = 1, ROWSPEC = "TId:%String,TCode:%String,TDesc:%String")
{
}

/// Generated by a.util.U5 at 2019-10-18 09:47:37
ClassMethod LookUpUserExecute(ByRef QHandle As %Library.Binary, q = "", onlyActive = 1, UseRes = "", LocId = "", CareType = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	s qU=$zcvt(q,"U")
	s nowDate=+$h
	if UseRes=1,LocId>0 {
		q:LocId="" $$$OK //走资源表 LocId必传

		Set ResId="" For  {
			Set ResId = $Order(^RB("RES",0,"CTLOC",LocId,ResId))
			Quit:ResId="" 
			continue:'$d(^RB("RES",ResId))
			Set ResCTPCPDR = $p(^RB("RES",ResId),"^",2)	;
			Set startdate=$p($G(^RB("RES",ResId)),"^",22)  //关联的开始日期与结束日期 
			set enddate=$p($G(^RB("RES",ResId)),"^",23)
			Continue:(startdate'="")&&(startdate>nowDate)
			Continue:(enddate'="")&&(enddate<nowDate)
			If ResCTPCPDR>0{
				if ($d(^CTPCP(ResCTPCPDR,1))=1) || ($d(^CTPCP(ResCTPCPDR,1))=11){
					Set CarPrvTpDR = $p(^CTPCP(ResCTPCPDR,1),"^",4) ;CTPCP_CarPrvTp_DR-->CT_CarPrvTp
					Set Type = $p(^CT("CPT",CarPrvTpDR),"^",4)		;CTCPT_InternalType
					continue:(CareType'="")&&(CareType'=Type)
					s TId=0
					for {
						Set TId = $o(^SSU("SSUSR",0,"CTPCP",ResCTPCPDR,TId))
						q:TId=""
						continue:TId'>0
						d outuser
					}
					
				}
			}
		}
	}else{
		s TId=0
		for {
			s TId=$o(^SSU("SSUSR",TId))	
			q:TId=""
			continue:TId'>0
			s CareProv=$p(^SSU("SSUSR",TId),"^",14),CarPrvTpDR="",Type=""
			if CareProv>0 s CarPrvTpDR = $p($g(^CTPCP(CareProv,1)),"^",4) ;CTPCP_CarPrvTp_DR-->CT_CarPrvTp
			if CarPrvTpDR>0 s Type = $p(^CT("CPT",CarPrvTpDR),"^",4)		;CTCPT_InternalType
			continue:(CareType'="")&&(CareType'=Type)
			d outuser
		}
	}

	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
outuser
	s g=^SSU("SSUSR",TId)
	s TCode=$p(g,"^",1)
	s TDesc=$p(g,"^",2)
	if useTrans s TDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.SSUser","SSUSRName",TDesc,langId)
	if onlyActive=1{
		s TDateFrom=$p(g,"^",96)
		s TDateTo=$p(g,"^",97)
		q:(TDateFrom>0)&&(TDateFrom>nowDate)
		q:(TDateTo>0)&&(TDateTo<nowDate)
		q:$p(g,"^",19)="N"
	}
	s TSpell=##class(ext.util.String).ToChineseSpell(TDesc)
	q:(q'="")&&($zcvt(TCode_","_TDesc_","_TSpell,"U")'[qU)
	
	Set ^CacheTemp(repid,ind)=$lb(TId,TCode,TDesc)
	set ind=ind+1
}

/// 查询某就诊之后的该病人的就诊记录
/// debug:d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","FindNextAdm","","")
Query FindNextAdm(adm = "") As websys.Query(CONTAINID = 0, ROWSPEC = "AdmType, AdmDate, AdmTime, CurDept, MainDoc, Diagnosis, DisDate, DisTime,EpisodeID,MyAdmType,MRAdm,AdmDT")
{
}

ClassMethod FindNextAdmExecute(ByRef QHandle As %Library.Binary, adm = "") As %Library.Status
{
	Set repid=$I(^CacheTemp)
	set ind=1 s QHandle=$lb(0,repid,0) 
	
	q:(adm="")||'$d(^PAADM(adm)) $$$OK
	s PatientID=$p(^PAADM(adm),"^",1)
	
	s lstEpisode = ##class(%ResultSet).%New("EPRservice.browser.BOEpisodeInfo:GetPatientList")
	s sc = lstEpisode.Execute(PatientID,"","")
	While (lstEpisode.Next()) {
		s AdmType = lstEpisode.GetDataByName("AdmType")
		s EpisodeID=lstEpisode.GetDataByName("EpisodeID")
		continue:EpisodeID'>adm //只取此次就诊之后的 
		if AdmType="E" {
			//set EIFlag=##class(web.DHCADMVisitStat).GetIfStaying(EpisodeID)
			set EIFlag=##class(web.DHCADMVisitStat).GetStayStatus(EpisodeID)
			set AdmType=$s(EIFlag>0:"EI",1:"EO")
		}
		s MRAdm=$P(^PAADM(EpisodeID),"^",61)
		s AdmDT=$zd(lstEpisode.GetData(2),3)_" "_$zt(lstEpisode.GetData(3))
		s tempList(EpisodeID)=$lb(lstEpisode.GetData(1),lstEpisode.GetData(2),lstEpisode.GetData(3),lstEpisode.GetData(4),lstEpisode.GetData(5),lstEpisode.GetData(6),lstEpisode.GetData(7),lstEpisode.GetData(8),lstEpisode.GetData(9),AdmType,MRAdm,AdmDT)
		
		
	}
	do lstEpisode.%Close()
	
	s EpisodeID=""
	for {
		s EpisodeID=$o(tempList(EpisodeID))
		q:EpisodeID=""
		Set ^CacheTemp(repid,ind)=tempList(EpisodeID)
		Set ind=ind+1
	}
	
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
}

/// 获取医嘱信息 返回BSP.SYS.COM.ProxyObject对象
ClassMethod GetOneOrdInfoObj(ordItemId) As BSP.SYS.COM.ProxyObject
{
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	s ordPar=+ordItemId,ordSub=$p(ordItemId,"||",2)
	s str1=^OEORD(ordPar,"I",ordSub,1)
	s ordAddDate=$p(^OEORD(ordPar,"I",ordSub,3),"^",7) //开医嘱日期 OEORI_Date
	s ordAddTime=$p(str1,"^",17) //开医嘱时间 OEORI_TimeOrd
	s ordAddDT=##class(websys.Conversions).DateLogicalToHtml(ordAddDate)_" "_$zt(ordAddTime) 
	s arcim=$p(str1,"^",2)
	s arcimdesc=$p(^ARCIM(+arcim,$p(arcim,"||",2),1),"^",2) //医嘱项名称
	
	s ordDoc=$p(str1,"^",11),ordDocName=""
	if ordDoc>0 s ordDocName=$p(^CTPCP(ordDoc,1),"^",2) //开医嘱人
	s ordLoc = $p($g(^OEORD(ordPar,"I",ordSub,7)),"^",2),ordLocDesc=""
	s:+ordLoc>0 ordLocDesc = $P($g(^CTLOC(ordLoc)),"^",2) //开单科室
	s ordRecLoc=$p(^OEORD(ordPar,"I",ordSub,3),"^",6),ordRecLocDesc=""
	s:ordRecLoc>0 ordRecLocDesc=$P($g(^CTLOC(ordRecLoc)),"^",2) //医嘱接受科室
	
	s str2=$g(^OEORD(ordPar,"I",ordSub,2))
	s ordDoseQty=$p(str2,"^",1) //单次剂量
	s ordDoseUnitID=$p(str2,"^",3),ordDoseUnit=""
	s:ordDoseUnitID'="" ordDoseUnit=$p(^CT("UOM",ordDoseUnitID),"^",2) //单次剂量单位
	
	s ordPHFreqDR=$p(str2,"^",4),ordPHFreq="" //频次
	i (ordPHFreqDR'="") {
		s IsCNMedItem=##class(web.DHCDocOrderCommon).IsCNMedItem(arcim)
		if (IsCNMedItem=1){
			s ordPHFreq=$p(^PHCFR(ordPHFreqDR),"^",3)
			if useTrans s ordPHFreq=##class(web.DHCAntCVComm).GetTranByDesc("User.PHCFreq","PHCFRDesc1",ordPHFreq,langId)
		}else{
			s ordPHFreq=$p(^PHCFR(ordPHFreqDR),"^",4)
			
			if (ordPHFreq="饮片"){
				s ordPHFreq=$p(^PHCFR(ordPHFreqDR),"^",3)
				if useTrans s ordPHFreq=##class(web.DHCAntCVComm).GetTranByDesc("User.PHCFreq","PHCFRDesc1",ordPHFreq,langId)
			}else{
				if useTrans s ordPHFreq=##class(web.DHCAntCVComm).GetTranByDesc("User.PHCFreq","PHCFRDesc2",ordPHFreq,langId)	
			}
		}
	}
	
	s ordInstrDr=$p(str2,"^",7),ordInstr="" //用法
	s:$g(ordInstrDr)'="" ordInstr=$p($g(^PHCIN(ordInstrDr)),"^",2)
	
	s ordDuraDR=$p(str2,"^",6),ordDura="" //疗程
	s:$g(ordDuraDR)'="" ordDura=$p(^PHCDU(ordDuraDR),"^",3)
	
	if useTrans {
		d TransData
	}
	
	
	s ordObj=##class(BSP.SYS.COM.ProxyObject).%New()
	s ordObj.ordItemId=ordItemId
	s ordObj.ordDesc=arcimdesc //医嘱名称
	s ordObj.ordAddDate=ordAddDate //开立日期
	s ordObj.ordAddTime=ordAddTime //开立时间
	s ordObj.ordAddDT=ordAddDT
	s ordObj.ordDocName=ordDocName //开医嘱人
	s ordObj.ordLocDesc=ordLocDesc //开医嘱科室
	s ordObj.ordRecLocDesc=ordRecLocDesc //医嘱接收科室
	s ordObj.ordDoseQty=ordDoseQty //单次剂量
	s ordObj.ordDoseUnit=ordDoseUnit //单次剂量单位
	s ordObj.ordPHFreq=ordPHFreq //频次
	s ordObj.ordInstrDr=ordInstr //用法
	s ordObj.ordDura=ordDura //疗程
	q ordObj
TransData
	s arcimdesc=##class(web.DHCAntCVComm).GetTranByDesc("User.ARCItmMast","ARCIMDesc",arcimdesc,langId)
	s ordDocName=##class(web.DHCAntCVComm).GetTranByDesc("User.CTCareProv","CTPCPDesc",ordDocName,langId)
	s ordLocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",ordLocDesc,langId)
	s ordRecLocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",ordRecLocDesc,langId)
	s ordDoseUnit=##class(web.DHCAntCVComm).GetTranByDesc("User.CTUOM","CTUOMDesc",ordDoseUnit,langId)
	s ordPHFreq=ordPHFreq
	s ordInstr=##class(web.DHCAntCVComm).GetTranByDesc("User.PHCInstruc","PHCINDesc1",ordInstr,langId)
	s ordDura=##class(web.DHCAntCVComm).GetTranByDesc("User.PHCDuration","PHCDUDesc1",ordDura,langId)
	q
}

/// 获取就诊信息 返回BSP.SYS.COM.ProxyObject对象
ClassMethod GetAdmInfoObj(adm) As BSP.SYS.COM.ProxyObject
{
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	s obj=##class(BSP.SYS.COM.ProxyObject).%New()
	s (admType,admLoc,admLocDesc,admDoc,admDocName,admDate)=""
	s admType=$p(^PAADM(adm),"^",2)
	s admLoc=$p(^PAADM(adm),"^",4)
	i admLoc>0 s admLocDesc=$p(^CTLOC(admLoc),"^",2)
	s admDoc=$p(^PAADM(adm),"^",9)
	i admDoc>0 s admDocName=$p(^CTPCP(admDoc,1),"^",2)
	s admDate=$P($G(^PAADM(adm)),"^",6)
	i admDate>0 s admDate=##class(websys.Conversions).DateLogicalToHtml(admDate)
	s admTime=$P($G(^PAADM(adm)),"^",7)
	i admTime>0 s admTime=$zt(admTime)
	set mradm=$P(^PAADM(adm),"^",61)
	
	if useTrans {
		d TransData
	}
	
	d obj.%Set("adm",adm).%Set("admType",admType).%Set("admLocDesc",admLocDesc).%Set("admLoc",admLoc)
	d obj.%Set("admDocName",admDocName).%Set("admDate",admDate).%Set("admTime",admTime).%Set("mradm",mradm)
	
	s patMrNo=##Class(DHCWMR.IO.OutService).IGetMrNoByEpisodeID(adm,admType,.ErrMsg)
	d obj.%Set("patMrNo",patMrNo)
	
	q obj
TransData
	s admDocName=##class(web.DHCAntCVComm).GetTranByDesc("User.CTCareProv","CTPCPDesc",admDocName,langId)
	s admLocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",admLocDesc,langId)
	q
}

/// 获取就诊信息 返回BSP.SYS.COM.ProxyObject对象
ClassMethod GetPatInfoObj(patientID, adm = "") As BSP.SYS.COM.ProxyObject
{
	s obj=##class(BSP.SYS.COM.ProxyObject).%New()
	s (patName,sexDr,gender,age,DOB,PAPMINO)=""
	s patName=$p(^PAPER(patientID,"ALL"),"^",1)
	s sexDr=$p(^PAPER(patientID,"ALL"),"^",7)
	i sexDr>0 s gender=$p(^CT("SEX",sexDr),"^",2)
	s age=##class(web.DHCBillInterface).GetPapmiAge(patientID,adm)
	s DOB=$p(^PAPER(patientID,"ALL"),"^",6)
	i DOB>0 s DOB=##class(websys.Conversions).DateLogicalToHtml(DOB)
	s PAPMINO=$p(^PAPER(patientID,"PAT",1),"^",1)
	s patPhone=$p(^PAPER(patientID,"PER",1),"^",11)  //PAPER_TelH
	d obj.%Set("patientID",patientID).%Set("patName",patName).%Set("gender",gender).%Set("DOB",DOB)
	d obj.%Set("age",age).%Set("PAPMINO",PAPMINO).%Set("patPhone",patPhone)
	q obj
}

/// 秒数是否在分钟时间段内
ClassMethod IsSecondsInMinRange(sec, minuteRange)
{
	if minuteRange="" q 1
	if sec="" q 0
	s len=$l(minuteRange,","),flag=0
	for i=1:1:len{
		s item=$p(minuteRange,",",i),s=+$p(item,"-",1)*60,e=+$p(item,"-",2)*60
		if s=0 s s=-9999999999
		if e=0 s e=9999999999
		if sec>s,sec<=e {
			s flag=1
			q 	
		}	
	}
	q flag
}

ClassMethod Seconds2Alias(sec)
{
	q:sec="" ""
	if sec<0 q ""
	s d=sec\86400,sec=sec#86400
	s h=sec\3600,sec=sec#3600
	s m=sec\60,sec=sec#60
	s ret=""
	if d>0 s ret=d_"天"
	if (h>0)||(ret'="") s ret=ret_h_"小时"
	if (m>0)||(ret'="") s ret=ret_m_"分钟"
	s ret=ret_sec_"秒"
	q ret
}

/// 获取患者某时某刻所在科室
/// w ##class(web.DHCAntCVComm).GetAdmLocation(2049,"2021-01-14","15:36:00")
ClassMethod GetAdmLocation(Adm, Date, Time)
{
	s retLoc=""
	if Date'="" s Date=##class(websys.Conversions).DateHtmlToLogical(Date)
	if Time[":" s Time=$zth(Time)
	s date=""
	if Date>0 s date=Date+1
	for {
		s date=$o(^PAADMi("TransDateTime1",Adm,date),-1)
		q:date=""
		q:(retLoc>0)
		s time=""
		if date=Date,Time>0 s time=Time+1
		for {
			s time=$o(^PAADMi("TransDateTime1",Adm,date,time),-1)
			q:time=""
			q:(retLoc>0)
			s sub=""
			for {
				s sub=$o(^PAADMi("TransDateTime1",Adm,date,time,sub),-1)
				q:sub=""
				q:(retLoc>0)
				s str=^PAADM(Adm,"TRANS",sub)
				s loc=$p(str,"^",6) //科室
				if retLoc'>0 s retLoc=loc
				
				
			}
			
		}
	}
	if retLoc'>0 s retLoc=$p(^PAADM(Adm),"^",4)
	q retLoc
}

/// 查询患者此次就诊医嘱项
/// d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","LookUpAdmOrder","27450")
Query LookUpAdmOrder(Adm = "", OrdStartDate = "", OrdStartTime = "", q = "") As websys.Query(CONTAINID = 3, ROWSPEC = "ordItemId,ordDesc,ordDateTime,ordLocDesc,ordDoctorName,ordRecLocDesc")
{
}

ClassMethod LookUpAdmOrderExecute(ByRef QHandle As %Library.Binary, Adm = "", OrdStartDate = "", OrdStartTime = "", q = "") As %Library.Status
{
 	s repid=$I(^CacheTemp) 
 	s ind=1 s QHandle=$lb(0,repid,0)
 	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	
 	s ordPar=$o(^OEORD(0,"Adm",+Adm,""))
 	if OrdStartDate'="" s OrdStartDate=##class(websys.Conversions).DateHtmlToLogical(OrdStartDate)
 	if OrdStartTime[":" s OrdStartTime=$zth(OrdStartTime)
 	
 	s qU=$zcvt(q,"U")
 	
 	set ordSub=0
	for {
		set ordSub=$o(^OEORD(ordPar,"I",ordSub))
		q:ordSub=""
		continue:'$d(^OEORD(ordPar,"I",ordSub,1))
		s str1=(^OEORD(ordPar,"I",ordSub,1))
		s arcim=$p(str1,"^",2)
		s arcimdesc=$p($g(^ARCIM(+arcim,$p(arcim,"||",2),1)),"^",2)
		s ItemCatRowid=$p($g(^ARCIM(+arcim,$p(arcim,"||",2),1)),"^",10)
		s ItemCatDesc= $P(^ARC("IC",ItemCatRowid),"^",1)
		if $e(ItemCatDesc,$l(ItemCatDesc))="费" continue

		
		s ordDesc=arcimdesc
		s StatusRowId=$p(str1,"^",13),StatusCode=""
		i StatusRowId'="" s StatusCode=$p($g(^OEC("OSTAT",StatusRowId)),"^",1)
		continue:(StatusCode="U")||(StatusCode="C")  //作废  撤销
		
		s ordDoctorName="",ordDoctorUserDr=""
		s DoctorDr=$p(str1,"^",11)
		continue:DoctorDr="" //开单医生为空的不要
		if DoctorDr>0 s ordDoctorName=$p(^CTPCP(DoctorDr,1),"^",2),ordDoctorUserDr = $o(^SSU("SSUSR",0,"CTPCP",DoctorDr,""))
		
		s ordLocDesc=""
		s ordLoc = $p($g(^OEORD(ordPar,"I",ordSub,7)),"^",2)
		s:+ordLoc>0 ordLocDesc = $P($g(^CTLOC(ordLoc)),"^",2)
		if ordLocDesc["-" s ordLocDesc=$p(ordLocDesc,"-",2)
		
		s ordDate=$p($g(^OEORD(ordPar,"I",ordSub,3)),"^",7)
		s ordTime=$p(^OEORD(ordPar,"I",ordSub,1),"^",17)
		s ordDateTime=##class(websys.Conversions).DateLogicalToHtml(ordDate)_" "_$zt(ordTime)
		if OrdStartDate>0 {  //开医嘱日期时间过滤
			if (ordDate<OrdStartDate)||((OrdStartDate=ordDate)&&(ordTime<OrdStartTime)) continue
		}
		
		s ordItemId=ordPar_"||"_ordSub
		
		s ordRecLoc=$p($g(^OEORD(ordPar,"I",ordSub,3)),"^",6),ordRecLocDesc=""
		s:ordRecLoc>0 ordRecLocDesc= $P($g(^CTLOC(ordRecLoc)),"^",2)
		if ordRecLocDesc["-" s ordRecLocDesc=$p(ordRecLocDesc,"-",2)
		
		if useTrans {
			d TransData
		}
	
		if qU'="" {
			s spell=##class(ext.util.String).ToChineseSpell(ordDesc)
			continue:$zcvt(spell_","_ordDesc,"U")'[qU	
		}
		
		Set ^CacheTemp(repid,ind)=$lb(ordItemId,ordDesc,ordDateTime,ordLocDesc,ordDoctorName,ordRecLocDesc)
		Set ind=ind+1
	}
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
TransData
	s ordDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.ARCItmMast","ARCIMDesc",ordDesc,langId)
	s ordDoctorName=##class(web.DHCAntCVComm).GetTranByDesc("User.CTCareProv","CTPCPDesc",ordDoctorName,langId)
	s ordLocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",ordLocDesc,langId)
	s ordRecLocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",ordRecLocDesc,langId)
	q
}

ClassMethod GetUserIdByCode(usercode)
{

	s userid=""
	s usercodeU=$$ALPHAUP^SSUTIL4(usercode)
	if usercodeU'="" {
		s userid=$o(^SSU("SSUSR",0,"SSUSR_Initials",usercodeU,""))
	}
	q userid
}

/// w ##class(web.DHCAntCVComm).GetUserNameByCode("ys01")
ClassMethod GetUserNameByCode(usercode, useTrans = 0)
{
	s ret=""
	s userid=..GetUserIdByCode(usercode)
	if userid>0 s ret=$p($g(^SSU("SSUSR",userid)),"^",2)
	if useTrans=1 s ret=##class(web.DHCAntCVComm).GetTranByDesc("User.SSUser","SSUSRName",ret,"")
	q ret
}

ClassMethod GetUserPhone(userid)
{
	q:userid="" ""
	if $d(^SSU("SSUSR",userid)) {
		s userPhone=$p($g(^SSU("SSUSR",userid)),"^",99) //8.3.1+ 开始使用用户上的SSUSRMobile
		if userPhone="" {
			s userCTPCP=$p($g(^SSU("SSUSR",userid)),"^",14)
			if userCTPCP>0 {
				s userPhone=$p($g(^CTPCP(userCTPCP,3)),"^",6)
			}
		}
		q userPhone
	}
	q ""
}

/// 办公电话
ClassMethod GetUserWorkPhone(userid)
{
	q:userid="" ""
	if $d(^SSU("SSUSR",userid)) {
		s userPhone=$p($g(^SSU("SSUSR",userid)),"^",100) //8.3.1+ 开始使用用户上的SSUSRPager
		if userPhone="" {
			s userCTPCP=$p($g(^SSU("SSUSR",userid)),"^",14)
			if userCTPCP>0 {
				s userPhone=$p($g(^CTPCP(userCTPCP,2)),"^",1) //
			}
		}
		q userPhone
	}
	q ""
}

ClassMethod GetLocPhone(locid)
{
	q:locid="" ""
	s locPhone=$p($g(^CTLOC(locid)),"^",40)  //	CTLOC_Telephone
	q locPhone
}

ClassMethod GetTranByDesc(Cls, Prop, Desc, LangId = "")
{
	if ..#useTrans=0 q Desc
	if ##class(%Dictionary.MethodDefinition).%ExistsId("web.DHCBL.BDP.BDPConfig||GetConfigValue"){
		 Set Flag =##class(web.DHCBL.BDP.BDPConfig).GetConfigValue("BDPTranslation")
		 Quit:Flag="N" Desc
	}
	Q:'##class(%Dictionary.MethodDefinition).%ExistsId("web.DHCBL.Authorize.BDPTranslation||GetTransDesc") Desc
	
	If LangId="",$d(%session),$d(%session.Data("LOGON.LANGID")) {
		Set LangId = %session.Data("LOGON.LANGID")
	}
	If LangId="" Set LangId=20
	Set Languages=$p($g(^SS("LAN",LangId)),"^",1)
	Q ##class(web.DHCBL.Authorize.BDPTranslation).GetTransDesc(Cls,Prop,Languages,Desc)
 	Q Desc
}

ClassMethod GetPageTrans(page, desc, langId = "")
{
	if ..#useTrans=0 q desc
	

	If langId="",$d(%session),$d(%session.Data("LOGON.LANGID")) {
		Set langId = %session.Data("LOGON.LANGID")
	}
	If langId="" Set langId=20
	
	if $l($g(^oddCOM("websys.Translation","m","Get",50)),",")>2 {
		q ##class(websys.Translation).Get(page,desc,langId)
	}else{
		q ##class(websys.Translation).Get(page,desc)
	}
	q desc
}

/// 联系结果翻译 
ClassMethod GetConResultTrans(desc, langId = "")
{
	q ##class(web.DHCAntCVComm).GetPageTrans("criticalvalue.trans.hisui.csp",desc,langId)
}

/// 操作节点翻译
ClassMethod GetProcessTrans(process, langId = "")
{
	q ##class(web.DHCAntCVComm).GetPageTrans("criticalvalue.process.csp",process,langId)
}

/// 危急值类型翻译
ClassMethod GetCVTypeTrans(type, langId = "")
{
	q ##class(web.DHCAntCVComm).GetPageTrans("criticalvalue.search.csp",type,langId)
}

/// 危急值状态翻译
ClassMethod GetCVStatusTrans(status, langId = "")
{
	q ##class(web.DHCAntCVComm).GetPageTrans("criticalvalue.search.csp",status,langId)
}

ClassMethod TimeAliasTrans(alias, langId = "")
{
	if ..#useTrans=0 q alias
	
	if alias="" q ""
	
	if langId'=1 q alias  //仅支持翻英文
	s temp=alias
	
	if temp["天" s d=+$p(temp,"天",1),temp=$p(temp,"天",2)
	else  s d=0,temp=temp
	
	if temp["小时" s h=+$p(temp,"小时",1),temp=$p(temp,"小时",2)
	else  s h=0,temp=temp
	
	if temp["分钟" s m=+$p(temp,"分钟",1),temp=$p(temp,"分钟",2)
	else  s m=0,temp=temp
	
	if temp["秒" s sec=+$p(temp,"秒",1)
	else  s sec=0
	
	if langId=1 {
		s ret=""
		if d>0 s ret=d_" day"_$s(d>1:"s",1:"")
		if h>0 s ret=ret_$s(ret="":"",1:", ")_h_" hour"_$s(h>1:"s",1:"")
		if m>0 s ret=ret_$s(ret="":"",1:", ")_m_" minute"_$s(m>1:"s",1:"")
		if sec>0 {
			s ret=ret_$s(ret="":"",1:", ")_sec_" second"_$s(sec>1:"s",1:"")
		}else{
			if ret="" s ret="0 second"	
		}
		
		s len=$l(ret,", ")
		if len>1 {
			s ret=$p(ret,", ",1, len-1)_" and "_$p(ret,", ",len)
		}
		q ret
	}
}

/// 获取session变量值
ClassMethod GetSessionVariable(key)
{

	q:key="" ""
	if $d(^||CVSessionData(key)){
		q ^||CVSessionData(key)
	}
	if $d(%session) {
		if $d(%session.Data("CV."_key)) {
			q %session.Get("CV."_key)
		}else{
			q %session.Get(key)	
		}
	}
	q ""
}

ClassMethod SetCVSession(key, value, permanent = 0)
{
	if permanent=1 {
		if $d(%session) {
			d %session.Lock()
			s %session.Data("CV."_key)=value	
		}
	}else{
		s ^||CVSessionData(key)=value
	}
	q ""
}

ClassMethod GetCPTByUserId(userid)
{
	q:userid="" ""
	s userCTPCP=$p($g(^SSU("SSUSR",userid)),"^",14)
	if userCTPCP>0 {
		Set CarPrvTpDR = $p($g(^CTPCP(userCTPCP,1)),"^",4) ;CTPCP_CarPrvTp_DR-->CT_CarPrvTp
		if CarPrvTpDR>0 q $p($g(^CT("CPT",CarPrvTpDR)),"^",4)		;CTCPT_InternalType
	}
	q ""
}

ClassMethod GetCPTByUserCode(usercode)
{
	s userid=..GetUserIdByCode(usercode)
	q ..GetCPTByUserId(userid)
}

/// w ##class(web.DHCAntCVComm).GetSpecName("1586||284")
ClassMethod GetSpecName(ordItem)
{
	s ordPar=+$p(ordItem,"||",1),ordSub=+$p(ordItem,"||",2)
	
	s ordLoc = $p($g(^OEORD(ordPar,"I",ordSub,7)),"^",2),hospId=""
	if ordLoc>0 s hospId=$p($g(^CTLOC(ordLoc)),"^",22)
	if hospId="" s hospId=$o(^CT("HOSP",0))
	
	s hospCode=$p(^CT("HOSP",hospId),"^",1),dboHospId=""
	
	s dboHospId=$o(^dbo.BTHospitalI("IndexCode",hospCode,0))
	
	
	
	//标本类型
	s SpecDr=$o(^OEORD(ordPar,"I",ordSub,"SPEC",""),-1)
	s (SpecCode,SpecName)=""
	i $l(SpecDr) s SpecCode=$p(^OEORD(ordPar,"I",ordSub,"SPEC",SpecDr),"^",1)
	i $l(SpecCode) {
		s SpecId=$o(^dbo.BTSpecimenI("IndexCode",dboHospId,$zcvt(SpecCode,"U"),""))
		if SpecId>0 {
			s SpecName=$lg($g(^dbo.BTSpecimenD(SpecId)),3)
			s SpecName=..GetTranByDesc("dbo.BTSpecimen","IName",SpecName,"")
		}
	}
	q SpecName
}

/// debug:d ##class(%ResultSet).RunQuery("web.DHCAntCVComm","FindRepLoc","","")
Query FindRepLoc(q As %String = "", HospId As %String = "") As websys.Query(CONTAINID = 0, ROWSPEC = "LocId,LocCode,LocDesc")
{
}

ClassMethod FindRepLocExecute(ByRef QHandle As %Library.Binary, q As %String = "", HospId As %String = "") As %Library.Status
{
	Set repid=$I(^CacheTemp)
	set ind=1 s QHandle=$lb(0,repid,0) 
	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	s qU=$zcvt(q,"U")
	
	s id=""
	for {
		s id=$o(^User.DHCAntCVOptionsD("RepLocData",id))
		q:id=""
		s code="",desc=""
		if $d(^CTLOC(id)) {
			s code=$p(^CTLOC(id),"^",1)
			s desc=$p(^CTLOC(id),"^",2)
			if useTrans s desc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",desc,langId) 
			s hosp=$p(^CTLOC(id),"^",22)
			continue:(HospId>0)&&(HospId'=hosp)
			
		}else{
			
			s desc=id
			if useTrans s desc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",desc,langId) 
			s code=desc
		}
		
		
		
		continue:(qU'="")&&($zcvt(code_","_desc,"U")'[qU)&&(##class(ext.util.String).ToChineseSpell(desc)'[qU)
		
		Set ^CacheTemp(repid,ind)=$lb(id,code,desc)
		Set ind=ind+1
	}
	
	Set QHandle=$lb(0,repid,0)
	Quit $$$OK
}

}
