Class web.DHCAntCVMsgCfg Extends %RegisteredObject
{

/// HIS消息平台是不是8.5及以上版本 
ClassMethod IsV85Plus()
{
	s isV85Plus=0
	if ##class(%Dictionary.ClassDefinition).%ExistsId("BSP.MSG.SRV.Interface"),$parameter("BSP.MSG.SRV.Interface","V")>=8.5 {
		s isV85Plus=1
	}
	q isV85Plus
}

/// 初始化方法
/// d ##class(web.DHCAntCVMsgCfg).Init()
ClassMethod Init()
{
	k ^User.DHCAntCVMsgCfgD
	k ^User.DHCAntCVMsgCfgI
	
	set HospId=0
	for {
		s HospId=$o(^CT("HOSP",HospId))
		q:HospId=""
		if HospId>0 d ..InitHosp(HospId)	
	}
}

ClassMethod InitHosp(HospId = "")
{
	d ..Save("","O","",1,"","","","","",HospId)
	d ..Save("","E","",1,"","","","","",HospId)
	d ..Save("","I","",1,"","","","","",HospId)
	d ..Save("","H","",1,"","","","","",HospId)
	d ..Save("","O","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctor",HospId,"","","I")
	d ..Save("","E","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctorNurse",HospId,"","","I")
	d ..Save("","I","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctorNurse",HospId,"","","I")
	d ..Save("","H","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctor",HospId,"","","I")
	if ..IsV85Plus() { //消息平台8.5之上才
		d ..Save("","O","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctor",HospId,"","","HCCS")
		d ..Save("","E","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctorNurse",HospId,"","","HCCS")
		d ..Save("","I","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctorNurse",HospId,"","","HCCS")
		d ..Save("","H","",1,0,"1,2,3,4,5,6","00:00:00","23:59:59","M-WardDoctor",HospId,"","","HCCS")
	}
}

ClassMethod CheckAndInit()
{
	set HospId=0
	for {
		s HospId=$o(^CT("HOSP",HospId))
		q:HospId=""
		if HospId>0,'$d(^User.DHCAntCVMsgCfgI("HTLT",HospId)) {
			d ..InitHosp(HospId)
		}
	}
}

/// d ##class(web.DHCAntCVMsgCfg).FixOldData()
ClassMethod FixOldData()
{
	s HOSPID=$o(^CT("HOSP",0))
	if HOSPID>0 {
		&sql(update SQLUser.DHC_AntCVMsgCfg set CVMC_AdmHospital=:HOSPID )
	}
}

/// 检查消息平台处1000的动作类型配置
ClassMethod CheckMsgAction(ActionTypeCode = "1000")
{
	set ActionRowId=$o(^websys.DHCMessageActionTypeI("ActionCode"," "_ActionTypeCode,""))
	if ActionRowId="" q "-1"
	set ReceiveRowId=$lg(^websys.DHCMessageActionTypeD(ActionRowId),4)
	if ReceiveRowId>0 q "-2"
	q "1"
}

ClassMethod FixMsgAction(ActionTypeCode = "1000")
{
	set ActionRowId=$o(^websys.DHCMessageActionTypeI("ActionCode"," "_ActionTypeCode,""))
	if ActionRowId>0{
		s obj=##class(websys.DHCMessageActionType).%OpenId(ActionRowId)
		d obj.DHCActionReceiveTypeDrSetObjectId("")
		d obj.%Save()
		d obj.%Close()
		s obj=""
	}
	q 1
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod Save(Id = "", AdmType = "", AdmLoc = "", SendTimes = "", SendInterval = "", CVTypes = "", StartTime = "", EndTime = "", ReceiveCode = "", HospId = "", DaysOfWeek = "", TargetRole = "", SendMode = "", ResultContains = "")
{
	i StartTime="" s StartTime=0
	i StartTime[":" s StartTime=$zth(StartTime)
	i EndTime="" s EndTime="23:59:59"
	i EndTime[":" s EndTime=$zth(EndTime)
	
	s AdmLoc=+AdmLoc
	
	if SendMode="" s SendMode="I"
	
	If Id>0{
		Set obj = ##class(User.DHCAntCVMsgCfg).%OpenId(Id)
	}else{
		Set obj = ##class(User.DHCAntCVMsgCfg).%New()	
	}
	if $IsObject(obj){
		set obj.CVMCAdmLoc=AdmLoc
		set obj.CVMCAdmType=AdmType
		set obj.CVMCCVTypes=CVTypes
		set obj.CVMCEndTime=EndTime
		set obj.CVMCReceiveCode=ReceiveCode
		set obj.CVMCSendInterval=SendInterval
		set obj.CVMCSendTimes=SendTimes
		set obj.CVMCStartTime=StartTime
		set obj.CVMCAdmHospital=HospId
		set obj.CVMCDaysOfWeek=DaysOfWeek //add dow
		set obj.CVMCTargetRole=TargetRole
		set obj.CVMCSendMode=SendMode
		set obj.CVMCResultContains=ResultContains
		set sc = obj.%Save()
		if $$$ISERR(sc){
			Quit "-1^SaveFail"_$system.Status.GetErrorText(sc)
		}
		Set Id = obj.%Id()
		do obj.%Close()
		Set obj = ""
	}
	q Id
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod Delete(Id = "")
{
	If Id="" q "-1^IdIsNull"
	set sc = ##class(User.DHCAntCVMsgCfg).%DeleteId(Id)
	if $$$ISERR(sc){
		Quit "-1^DelFail"_$system.Status.GetErrorText(sc)
	}
	Quit Id
}

ClassMethod DeleteByAdmTypeLocTimes(key, HospId = "")
{
	s type=$p(key,"-",1)
	s loc=+$p(key,"-",2)
	s times=$p(key,"-",3)
	if times=""{  //删除科室配置
		q:loc=0 -1_"^不能删默认的科室配置" //不能删默认的科室配置
		&sql(
			delete from SQLUser.DHC_AntCVMsgCfg where CVMC_AdmType=:type And CVMC_AdmLoc=:loc and CVMC_AdmHospital=:HospId
		)
		i SQLCODE=0 q 1
		else  q "-1^SQLCODE="_SQLCODE
	}else{
		q:times=1 -1_"^不能删除第一次发送配置"
		&sql(
			delete from SQLUser.DHC_AntCVMsgCfg where CVMC_AdmType=:type And CVMC_AdmLoc=:loc And CVMC_SendTimes=:times and CVMC_AdmHospital=:HospId
		)
		i SQLCODE=0 q 1
		else  q "-1^SQLCODE="_SQLCODE
	}
}

ClassMethod LazyTree(id = "", HospId = "")
{
	if id="" {
		w "["
		w "{""id"":""O"",""text"":""门诊"",""state"":""closed""}"
		w ",{""id"":""E"",""text"":""急诊"",""state"":""closed""}"
		w ",{""id"":""I"",""text"":""住院"",""state"":""closed""}"
		w ",{""id"":""H"",""text"":""体检"",""state"":""closed""}"
		w "]"
		q ""
	}
	if $l(id)=1 {
		s type=id
		w "["
		set loc=""
		for {
			s loc=$o(^User.DHCAntCVMsgCfgI("HTLT",+HospId,type,loc))
			q:loc=""
			if loc=0 {
				w "{""id"":"""_id_"-"_loc_""",""text"":""默认"",""state"":""closed""}"
			}else {
				s locdesc=$p($g(^CTLOC(loc)),"^",2)
				w ",{""id"":"""_id_"-"_loc_""",""text"":"""_locdesc_""",""state"":""closed""}"
			}
		}
		w "]"
		q ""
	}
	if $l(id,"-")=2 {
		s type=$p(id,"-",1),loc=+$p(id,"-",2)
		w "["
		set times=0
		for {
			s times=$o(^User.DHCAntCVMsgCfgI("HTLT",+HospId,type,loc,times))
			q:times=""
			if times=1 {
				w "{""id"":"""_id_"-"_times_""",""text"":""第"_times_"次"",""state"":""open""}"
			}else {
				w ",{""id"":"""_id_"-"_times_""",""text"":""第"_times_"次"",""state"":""open""}"
			}
		}
		w "]"
		q ""
	}
	
	w "[]" q ""
}

ClassMethod CVTypeCode2Desc(Code)
{
	//1:检验,2病理,3心电,4超声,5内镜,6放射
	s str="检验,病理,心电,超声,内镜,放射"
	for i=1:1:$l(Code,","){
		s $p(Code,",",i)=##class(web.DHCAntCVReportNameQuery).GetPanicName($p(Code,",",i)) //$p(str,",",$p(Code,",",i) )
	}
	q Code
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVMsgCfg","Find")
Query Find(AdmType = "", AdmLoc = "", SendTimes = "", HospId = "") As websys.Query(CONTAINID = 1, ROWSPEC = "TId:%String,TAdmLoc:%String,TAdmType:%String,TCVTypes:%String,TEndTime:%String,TReceiveCode:%String,TSendInterval:%String,TSendTimes:%String,TStartTime:%String,TCVTypesDesc,TReceiveDesc,TDaysOfWeek,TTargetRole,TTargetRoleDesc,TSendMode,TSendModeDesc,TResultContains")
{
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod FindExecute(ByRef QHandle As %Library.Binary, AdmType = "", AdmLoc = "", SendTimes = "", HospId = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	q:(AdmType="")||(AdmLoc="")||(SendTimes="")||(HospId="") $$$OK
	
	
#;	s type=""
#;	for {
#;		s type =$o(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes",type))
#;		q:type=""
#;		continue:(AdmType'="")&&(AdmType'=type)
#;		s loc=""
#;		for {
#;			s loc =$o(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes",type,loc))
#;			q:loc=""
#;			continue:(AdmLoc'="")&&(AdmLoc'=loc)
#;			s times=0
#;			for {
#;				s times =$o(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes",type,loc,times))
#;				q:times=""
#;				continue:(SendTimes'="")&&(SendTimes'=times)
				s type=AdmType,loc=AdmLoc,times=SendTimes,hospital=HospId
				s cfgid=""
				for {
					s cfgid =$o(^User.DHCAntCVMsgCfgI("HTLT",hospital,type,loc,times,cfgid))
					q:cfgid=""				
					d BuildSort
				}
#;				
#;			}
#;		}
#;	}
	///排序输出
	set type=""
	for {
		s type=$o(SortArr(type))
		q:type=""
		set loc=""
		for {
			s loc=$o(SortArr(type,loc))
			q:loc=""
			set times=""
			for {
				s times=$o(SortArr(type,loc,times))
				q:times=""
				s cvtypes=""
				for {
					s cvtypes=$o(SortArr(type,loc,times,cvtypes))
					q:cvtypes=""
					s sttime=""
					for {
						s sttime=$o(SortArr(type,loc,times,cvtypes,sttime))
						q:sttime=""
						s myind=""
						for {
							s myind=$o(SortArr(type,loc,times,cvtypes,sttime,myind))
							q:myind=""
							Set ^CacheTemp(repid,ind)=SortArr(type,loc,times,cvtypes,sttime,myind)
							set ind=ind+1
						}
					}
				}
			}
		}
	}
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
BuildSort	
	set g=^User.DHCAntCVMsgCfgD(cfgid)
	set TAdmLoc=$lg(g,3)
	set TAdmType=$lg(g,2)
	set TCVTypes=$lg(g,6)
	set TEndTime=$lg(g,8)
	set TReceiveCode=$lg(g,9)
	set TSendInterval=$lg(g,5)
	set TSendTimes=$lg(g,4)
	set TStartTime=$lg(g,7)
	set TCVTypesDesc=..CVTypeCode2Desc(TCVTypes)
	set TStartTime=$zt(TStartTime)
	set TEndTime=$zt(TEndTime)
	set TReceiveDesc=..ReceiveCode2Desc(TReceiveCode)
	q:(TCVTypes="")||(TStartTime="")
	set TDaysOfWeek=$lg(g,12)
	set TTargetRole=$lg(g,14)
	set TTargetRoleDesc=..TargetRoleCode2Desc(TTargetRole)
	set TSendMode=$lg(g,13)
	if TSendMode="" set TSendMode="I"
	set TSendModeDesc=..SendModeCode2Desc(TSendMode)
	set TResultContains=$lg(g,15)
	set SortArr(TAdmType,TAdmLoc,TSendTimes,TCVTypes,TStartTime,$i(SortArr))=$lb(cfgid,TAdmLoc,TAdmType,TCVTypes,TEndTime,TReceiveCode,TSendInterval,TSendTimes,TStartTime,TCVTypesDesc,TReceiveDesc,TDaysOfWeek,TTargetRole,TTargetRoleDesc,TSendMode,TSendModeDesc,TResultContains)
	q
}

ClassMethod SendModeCode2Desc(Codes)
{
	s ret=""
	do ##class(websys.DHCMessageConfig).BuildSendMode(.SendModeArr)
	s len=$l(Codes,",")
	for i=1:1:len {
		s Code=$p(Codes,",",i)
		if Code'="" s $p(ret,",",i)=$g(SendModeArr(Codes))
	}
	q ret
}

ClassMethod TargetRoleCode2Desc(Code)
{
	s desc=$case(Code,"":"自动判断","AdmLoc":"就诊科室","OrdLoc":"下医嘱科室","Any":"任何角色",:"")
	if desc=""{
		s loc="",group=""
		if Code["L" s loc=+$p(Code,"L",2)
		if Code["G" s group=+$p(Code,"G",2)
		if loc>0 s desc=$p($g(^CTLOC(loc)),"^",2)
		if group>0 s desc=desc_$s(loc>0:",",1:"")_$p($g(^SSU("SSGRP",group)),"^",1)
	}
	q desc
}

ClassMethod ReceiveCode2Desc(Code)
{
	s type=$p(Code,"-",1)
	s id=$p(Code,"-",2)
	q:id="" ""
	i type="M" {   //消息接收对象维护的接收对象
		s recdr=$o(^websys.DHCMessageReceiveTypeI("ReceiveCode"," "_$zcvt(id,"U"),0))
		if recdr>0 q $lg(^websys.DHCMessageReceiveTypeD(recdr),3)
		else  q ""
	}elseif type="L"{
		q "科室-"_$p($g(^CTLOC(id)),"^",2)
	}elseif type="LTEL"{
		q "科室电话-"_$p($g(^CTLOC(id)),"^",2)
	}elseif type="U"{
		q "用户-"_$p($g(^SSU("SSUSR",id)),"^",2)
	}elseif type="G"{
		q "安全组-"_$p($g(^SSU("SSGRP",id)),"^",1) //安全 2020-08-10
	}else{
		q ""	
	}
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVMsgCfg","FindLoc")
/// desc 查询条件
/// exceptexist  1 排除已存在配置的科室
/// admType 指定AdmType时,从User.PACAdmTypeLocation取科室  【基础数据-病人管理-访问类型位置】
Query FindLoc(desc = "", exceptexist = 0, admType = "", HospId = "") As websys.Query(CONTAINID = 1, ROWSPEC = "LocId,LocDesc,LocCode,LocHosp,LocHospDesc")
{
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod FindLocExecute(ByRef QHandle As %Library.Binary, desc = "", exceptexist = 0, admType = "", HospId = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	s descU=$zcvt(desc,"U")
	
	s useTrans=0,langId=20
	if $d(%session),%session.Get("LOGON.LANGID")>0 s useTrans=1,langId=%session.Get("LOGON.LANGID")
	
	if admType=""{   //为空 走CTLoc
		s LocId=0
		for {
			s LocId=$o(^CTLOC(LocId))
			q:LocId=""
			d Output
		}
	}else{  //不为空 走PACAdmTypeLocation
		for i=1:1:$l(admType){
			set type=$e(admType,i)
			s LocId=0
			for {
				set LocId=$o(^PAC("ADMLOC",0,"AdmType",type,LocId))	
				q:LocId=""
				continue:'$d(^CTLOC(LocId))
				d Output
			}
		}
	}

	

	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
Output
	s ContactName=$p(^CTLOC(LocId),"^",43)
	s LocCode=$p(^CTLOC(LocId),"^",1)
	s LocDesc=$p(^CTLOC(LocId),"^",2)
	
	if useTrans s LocDesc=##class(web.DHCAntCVComm).GetTranByDesc("User.CTLoc","CTLOCDesc",LocDesc,langId)  //翻译
	
	
	q:$zcvt(ContactName_","_LocCode_","_LocDesc,"U")'[descU
	if exceptexist=1{
		q:($d(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes","I",LocId)))||($d(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes","E",LocId)))||($d(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes","O",LocId)))
	}
	s LocHosp=$p(^CTLOC(LocId),"^",22) //科室对应医院
	q:(HospId'="")&&(HospId'=LocHosp) //医院过滤
	
	set LocHospDesc=""
	if LocHosp>0 set LocHospDesc=$p($g(^CT("HOSP",LocHosp)),"^",2)
	
	Set ^CacheTemp(repid,ind)=$lb(LocId,LocDesc,LocCode,LocHosp,LocHospDesc)
	set ind=ind+1
}

Query FindUser(desc As %Library.String, active = "Y", HospId = "") As websys.Query(CONTAINID = 0, ROWSPEC = "UserName:%String,HIDDEN:%String,UserCode:%String")
{
}

ClassMethod FindUserExecute(ByRef QHandle As %Library.Binary, desc As %Library.String = "", active = "Y", HospId = "") As %Library.Status
{
	Set repid=$I(^CacheTemp)
	s ind=1
	Set descUp = $zcvt(desc,"U")
	set UserId=0,nowDate = +$h
	for {
		Set UserId = $O(^SSU("SSUSR",UserId))
		quit:UserId=""
		Set UserCode = $p(^SSU("SSUSR",UserId),"^",1)
		Set UserName = $p(^SSU("SSUSR",UserId),"^",2)
		Set UserActive = $p(^SSU("SSUSR",UserId),"^",19)
		Set DateFrom = $p(^SSU("SSUSR",UserId),"^",96)
		Set DateTo = $p(^SSU("SSUSR",UserId),"^",97)
		if DateFrom="" set DateFrom=nowDate-1
		if DateTo="" set DateTo=nowDate+1
		if (active=UserActive)&&(UserActive="Y"){ //查询有效的用户
			if (nowDate>DateFrom)&&(nowDate<DateTo){
				d Outputrow3
			}
		}
		if (active=UserActive)&&(UserActive="N"){ //只查询禁用的用户
			d Outputrow3
		}
		IF (active="ALL"){ //查询所有用户
			D Outputrow3
		}
	}
	Set QHandle=$lb(0,repid,0)
	Quit $$$OK
Outputrow3
	Set UserPY = ##class(ext.util.String).ToChineseSpell(UserName)
	if (($zcvt(UserCode,"U")[descUp) || ($zcvt(UserName,"U")[descUp) || (UserPY[descUp)){
		q:(HospId>0)&&(##class(%Dictionary.MethodDefinition).%ExistsId("BSP.GRPHOSP.SRV.Interface||GetDataShowFlag"))&&(##class(BSP.GRPHOSP.SRV.Interface).GetDataShowFlag("SS_User",UserId,HospId)=0) //增加院区过滤
		Set ^CacheTemp(repid,ind)=$lb(UserName,UserId,UserCode)
		set ind = ind+1
	}
	quit
}

/// d ##class(%ResultSet).RunQuery("websys.DHCMessageReceiveCfgMgr","FindGroup")
/// desc 查询条件
/// 查安全组
Query FindGroup(desc = "") As websys.Query(CONTAINID = 1, ROWSPEC = "TId,TCode,TDesc")
{
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod FindGroupExecute(ByRef QHandle As %Library.Binary, desc = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	s descU=$zcvt(desc,"U")
	
	s TId=0
	for {
		s TId=$o(^SSU("SSGRP",TId))
		q:TId=""
		s TDesc=$p(^SSU("SSGRP",TId),"^",1)	
		s TCode=TDesc
		d Output
	}

	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
Output
	q:$zcvt(TCode_","_TDesc_","_##class(ext.util.String).ToChineseSpell(TDesc),"U")'[descU
	Set ^CacheTemp(repid,ind)=$lb(TId,TCode,TDesc)
	set ind=ind+1
}

/// d ##class(%ResultSet).RunQuery("web.DHCAntCVMsgCfg","FindReceiveObj")
/// 选择接收者Query
Query FindReceiveObj(desc = "", rectype = "", HospId = "") As websys.Query(CONTAINID = 1, ROWSPEC = "ID,Code,Desc")
{
}

/// Generated by a.util.U5 at 2018-06-20 10:00:14
ClassMethod FindReceiveObjExecute(ByRef QHandle As %Library.Binary, desc = "", rectype = "", HospId = "") As %Library.Status
{
	s repid=$I(^CacheTemp)
	s ind=1
	s QHandle=$lb(0,repid,0)
	if rectype="M"{
		set rs=##class(%ResultSet).%New("websys.DHCMessageReceiveTypeMgr:FindAll")
		d rs.Execute("",desc)
		//DHCReceiveRowId  在页面中 直接用code作id
		s IDKey="DHCReceiveCode",CodeKey="DHCReceiveCode",DescKey="DHCReceiveDesc"
	}elseif (rectype="L")||( rectype="LTEL") {  //add科室电话
		set rs=##class(%ResultSet).%New("web.DHCAntCVMsgCfg:FindLoc")
		d rs.Execute(desc,"","",HospId)
		s IDKey="LocId",CodeKey="LocCode",DescKey="LocDesc"
	}elseif rectype="U" {
		set rs=##class(%ResultSet).%New("web.DHCAntCVMsgCfg:FindUser")
		d rs.Execute(desc,"Y",HospId)
		s IDKey="HIDDEN",CodeKey="UserCode",DescKey="UserName"
	}elseif rectype="G"{ 
		set rs=##class(%ResultSet).%New("web.DHCAntCVMsgCfg:FindGroup")
		d rs.Execute(desc)
		s IDKey="TId",CodeKey="TCode",DescKey="TDesc"
	}else{
		Quit $$$OK
	}
	while(rs.Next()){		
		s ID=rs.GetDataByName(IDKey)
		s Code=rs.GetDataByName(CodeKey)
		s Desc=rs.GetDataByName(DescKey)
		Set ^CacheTemp(repid,ind)=$lb(ID,Code,Desc)
		set ind=ind+1
	}
	Set QHandle=$lb(0,repid,0) 
	Quit $$$OK
}

ClassMethod GetLocTimes(type, loc, HospId = "")
{
	//s times =$o(^User.DHCAntCVMsgCfgI("AdmTypeLocTimes",type,loc,""),-1)
	s times =$o(^User.DHCAntCVMsgCfgI("HTLT",+HospId,type,loc,""),-1)
	q +times
}

}
